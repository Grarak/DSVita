uniform bool translucentOnly;

float3 in oColor : TEXCOORD0_HALF;
float2 in oTexCoords : TEXCOORD1;
float2 in texImageParamAddr : TEXCOORD2;
float2 in palPolyAttribAddr : TEXCOORD4;

uniform usampler2D tex : TEXUNIT0;
uniform sampler2D palTex : TEXUNIT1;
uniform usampler2D attrTex : TEXUNIT2;

unsigned short readTex8(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0;
    float y = float(addrY) / 255.0;
    unsigned int values[4];
    tex2D_gather4(tex, float2(x, y), values);
    short shift = addr & 3;
    return (values[0] >> (shift * 8)) & 0xFF;
}

unsigned short readTex16Aligned(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0;
    float y = float(addrY) / 255.0;
    unsigned int values[4];
    tex2D_gather4(tex, float2(x, y), values);
    short shift = addr & 2;
    return (values[0] >> (shift * 8)) & 0xFFFF;
}

float4 readPal(int index) {
    short indexX = index & 0x1FF;
    short indexY = index >> 9;
    float x = float(indexX) / 511.0;
    float y = float(indexY) / 95.0;
    return tex2D(palTex, float2(x, y));
}

float3 normRgb5(unsigned short color) {
    return float3(float(color & 0x1F), float((color >> 5) & 0x1F), float((color >> 10) & 0x1F)) / 31.0;
}

static const float4 TexelMulLookup[16] = {
    float4(1.0, 0.0, 0.0, 0.0), float4(0.0, 1.0, 0.0, 0.0), float4(0.0, 0.0, 1.0, 0.0), float4(-1.0, 0.0, 0.0, 0.0),
    float4(1.0, 0.0, 0.0, 0.0), float4(0.0, 1.0, 0.0, 0.0), float4(0.5, 0.5, 0.0, 0.0), float4(-1.0, 0.0, 0.0, 0.0),
    float4(1.0, 0.0, 0.0, 0.0), float4(0.0, 1.0, 0.0, 0.0), float4(0.0, 0.0, 1.0, 0.0), float4(0.0, 0.0, 0.0, 1.0),
    float4(1.0, 0.0, 0.0, 0.0), float4(0.0, 1.0, 0.0, 0.0), float4(5.0 / 8.0, 3.0 / 8.0, 0.0, 0.0), float4(3.0 / 8.0, 5.0 / 8.0, 0.0, 0.0),
};

float4 compressed4x4Tex(int palAddr, int addrOffset, short s, short t, short sizeS) {
    short tile = (t / 4) * (sizeS / 4) + (s / 4);
    int addr = addrOffset + (tile * 4 + (t & 0x3));

    short palIndex = readTex8(addr);
    palIndex = (palIndex >> ((s & 0x3) * 2)) & 0x3;

    addr = 0x20000 + (addrOffset & 0x1FFFF) / 2;
    if ((addrOffset >> 17) == 2) {
        addr += 0x10000;
    }
    short palBase = readTex16Aligned(addr + tile * 2);
    int palOffset = (palAddr << 3) + (palBase & 0x3FFF) * 2;

    float4 color0 = readPal(palOffset);
    float4 color1 = readPal(palOffset + 1);
    float4 color2 = readPal(palOffset + 2);
    float4 color3 = readPal(palOffset + 3);
    float4x4 colors = float4x4(
        float4(color0.rgb, 1.0),
        float4(color1.rgb, 1.0),
        float4(color2.rgb, 1.0),
        float4(color3.rgb, 1.0)
    );
    short mode = (palBase >> 14) & 0x3;
    short lookup = palIndex | (mode << 2);
    float4 weights = TexelMulLookup[lookup];
    if (weights.x < 0.0) {
        discard;
    }
    return float4(mul(weights, colors).rgb, 1.0);
}

float4 aXiXTex(int palAddr, int addrOffset, int s, int t, int sizeS, short aBits) {
    int addr = addrOffset + t * sizeS + s;

    short palIndex = readTex8(addr);
    if (palIndex == 0) {
        discard;
    }

    int palOffset = palAddr << 3;
    short colorBits = 8 - aBits;
    short colorMask = (1 << colorBits) - 1;
    short aMask = (1 << aBits) - 1;
    float aMax = float(aMask);
    float4 color = readPal(palOffset + (palIndex & colorMask));
    float alpha = float(short((palIndex >> colorBits) & aMask)) / aMax;
    return float4(color.rgb, alpha);
}

float4 directTex(int addrOffset, int s, int t, int sizeS) {
    int addr = addrOffset + (t * sizeS + s) * 2;
    short color = readTex16Aligned(addr);
    return float4(normRgb5(color), (color >> 15) == 0 ? 0.0 : 1.0);
}

float4 palXTex(int palAddr, int addrOffset, int s, int t, int sizeS, int format, bool transparent0) {
    int addr = addrOffset + ((t * sizeS + s) >> (2 - format));

    short palIndex = readTex8(addr);
    short mask1 = (4 >> format) - 1;
    short mask2 = (4 << ((format * 3) & 6)) - 1;
    palIndex = (palIndex >> ((s & mask1) * (2 << format))) & mask2;
    if (transparent0 && palIndex == 0) {
        discard;
    }

    int palOffset = palAddr << (format == 0 ? 2 : 3);
    float4 color = readPal(palOffset + palIndex);
    return float4(color.rgb, 1.0);
}

void main(out float4 color : COLOR) {
    int texImageParamValue = tex2D<unsigned int>(attrTex, texImageParamAddr);
    int palPolyAttribValue = tex2D<unsigned int>(attrTex, palPolyAttribAddr);

    unsigned int addrOffset = (texImageParamValue & 0xFFFF) << 3;
    unsigned short texImageParam = texImageParamValue >> 16;
    unsigned short palAddr = palPolyAttribValue & 0xFFFF;

    short sizeS = 8 << ((texImageParam >> 4) & 0x7);
    short sizeT = 8 << ((texImageParam >> 7) & 0x7);
    short s = short(oTexCoords.x);
    short t = short(oTexCoords.y);

    bool repeatS = (texImageParam & 0x1) == 1;
    bool repeatT = ((texImageParam >> 1) & 0x1) == 1;
    if (repeatS) {
        bool flip = ((texImageParam >> 2) & 0x1) == 1;
        if (flip && (s & sizeS) != 0) {
            s = -1 - s;
        }
        s &= sizeS - 1;
    } else if (s < 0) {
        s = 0;
    } else if (s >= sizeS) {
        s = sizeS - 1;
    }

    if (repeatT) {
        bool flip = ((texImageParam >> 3) & 0x1) == 1;
        if (flip && (t & sizeT) != 0) {
            t = -1 - t;
        }
        t &= sizeT - 1;
    } else if (t < 0) {
        t = 0;
    } else if (t >= sizeT) {
        t = sizeT - 1;
    }

    unsigned short polyAttr = palPolyAttribValue >> 16;
    float alpha = float(polyAttr & 31) / 31.0;

    short texFmt = (texImageParam >> 10) & 0x7;
    float4 texColor;
    if (texFmt == 0) {
        color = float4(oColor, alpha);
    } else if (texFmt == 1) {
        texColor = aXiXTex(palAddr, addrOffset, s, t, sizeS, 3);
    } else if (texFmt == 5) {
        texColor = compressed4x4Tex(palAddr, addrOffset, s, t, sizeS);
    } else if (texFmt == 6) {
        texColor = aXiXTex(palAddr, addrOffset, s, t, sizeS, 5);
    } else if (texFmt == 7) {
        texColor = directTex(addrOffset, s, t, sizeS);
    } else {
        bool transparent0 = ((texImageParam >> 13) & 0x1) == 1;
        texColor = palXTex(palAddr, addrOffset, s, t, sizeS, texFmt - 2, transparent0);
    }

    if (texFmt != 0) {
        short mode = (polyAttr >> 5) & 0x3;
        if (mode == 0) {
            color = texColor * float4(oColor, alpha);
        } else {
            color = texColor;
            color.a *= alpha;
        }
    }

    if (translucentOnly) {
        if (color.a == 1.0) {
            discard;
        }
    } else if (color.a != 1.0) {
        bool transNewDepth = ((polyAttr >> 7) & 1) != 0;
        if (transNewDepth) {
            color.a = 0.0;
        } else {
            discard;
        }
    }
}

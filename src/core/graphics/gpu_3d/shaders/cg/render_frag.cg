float3 in oColor : TEXCOORD0;
float2 in oTexCoords : TEXCOORD1;
float in oPolygonIndex : TEXCOORD2;

uniform sampler2D tex : TEXUNIT0;
uniform sampler2D palTex : TEXUNIT1;

typedef struct {
  int texImageParam;
  int palAddr;
} Polygon;

uniform PolygonUbo {
    Polygon polygons[2048];
} PolygonUbo : BUFFER[0];

short readTex8(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0;
    float y = float(addrY) / 255.0;
    return short(tex2D(tex, float2(x, y))[addr & 3] * 255.0);
}

short readTex16Aligned(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0;
    float y = float(addrY) / 255.0;
    float4 value = tex2D(tex, float2(x, y));
    short entry = short(addr & 2);
    return short(value[entry] * 255.0) | (short(value[entry + 1] * 255.0) << 8);
}

float4 readPal(short index) {
    short indexX = index & 0x1FF;
    short indexY = index >> 9;
    float x = float(indexX) / 511.0;
    float y = float(indexY) / 95.0;
    return tex2D(palTex, float2(x, y));
}

float3 normRgb5(short color) {
    return float3(float(color & 0x1F), float((color >> 5) & 0x1F), float((color >> 10) & 0x1F)) / 31.0;
}

float4 a3i5Tex(short index, int addrOffset, short s, short t, short sizeS) {
    int addr = addrOffset + t * sizeS + s;

    short palIndex = readTex8(addr);
    if (palIndex == 0) {
        discard;
    }

    short palOffset = PolygonUbo.polygons[index].palAddr << 3;
    float4 color = readPal(palOffset + (palIndex & 0x1F));
    float alpha = float((palIndex >> 5) & 0x3) / 7.0;
    return float4(color.rgb, alpha);
}

float4 pal4Tex(short index, int addrOffset, short s, short t, short sizeS, bool transparent0) {
    int addr = addrOffset + (t * sizeS + s) / 4;

    int palIndex = readTex8(addr);
    if (transparent0 && palIndex == 0) {
        discard;
    }
    palIndex = (palIndex >> ((s & 0x3) * 2)) & 0x03;

    int palOffset = PolygonUbo.polygons[index].palAddr << 2;
    float4 color = readPal(palOffset + palIndex);
    return float4(color.rgb, 1.0);
}

float4 pal16Tex(short index, int addrOffset, int s, int t, int sizeS, bool transparent0) {
    int addr = addrOffset + (t * sizeS + s) / 2;

    int palIndex = readTex8(addr);
    if (transparent0 && palIndex == 0) {
        discard;
    }
    palIndex = (palIndex >> ((s & 0x1) * 4)) & 0x0F;

    int palOffset = PolygonUbo.polygons[index].palAddr << 3;
    float4 color = readPal(palOffset + palIndex);
    return float4(color.rgb, 1.0);
}

float4 pal256Tex(short index, int addrOffset, int s, int t, int sizeS, bool transparent0) {
    int addr = addrOffset + (t * sizeS + s);

    int palIndex = readTex8(addr);
    if (transparent0 && palIndex == 0) {
        discard;
    }

    int palOffset = PolygonUbo.polygons[index].palAddr << 3;
    float4 color = readPal(palOffset + palIndex);
    return float4(color.rgb, 1.0);
}

float4 a5i3Tex(short index, int addrOffset, short s, short t, short sizeS) {
    int addr = addrOffset + t * sizeS + s;

    short palIndex = readTex8(addr);
    if (palIndex == 0) {
        discard;
    }

    int palOffset = PolygonUbo.polygons[index].palAddr << 3;
    float4 color = readPal(palOffset + (palIndex & 0x07));
    float alpha = float((palIndex >> 3) & 0x1F) / 31.0;
    return float4(color.rgb, alpha);
}

float4 directTex(int addrOffset, short s, short t, short sizeS) {
    int addr = (addrOffset + t * sizeS + s) * 2;
    int tex = readTex16Aligned(addr);
    if (tex == 0) {
        discard;
    }
    return float4(normRgb5(tex), 1.0);
}

void main(out float4 color : COLOR) {
    short polygonIndex = short(oPolygonIndex);
    int texImageParam = PolygonUbo.polygons[polygonIndex].texImageParam;

    int addrOffset = (texImageParam & 0xFFFF) << 3;
    short sizeS = 8 << ((texImageParam >> 20) & 0x7);
    short sizeT = 8 << ((texImageParam >> 23) & 0x7);
    short s = short(oTexCoords.x);
    short t = short(oTexCoords.y);

    bool repeatS = ((texImageParam >> 16) & 0x1) == 1;
    bool repeatT = ((texImageParam >> 17) & 0x1) == 1;
    if (repeatS) {
        bool flip = ((texImageParam >> 18) & 0x1) == 1;
        if (flip && (s & sizeT) != 0) {
            s = -s;
        }
        s += sizeS;
        s &= sizeS - 1;
    } else if (s < 0) {
        s = 0;
    } else if (s >= sizeS) {
        s = sizeS - 1;
    }

    if (repeatT) {
        bool flip = ((texImageParam >> 19) & 0x1) == 1;
        if (flip && (t & sizeT) != 0) {
            t = -t;
        }
        t += sizeT;
        t &= sizeT - 1;
    } else if (t < 0) {
        t = 0;
    } else if (t >= sizeT) {
        t = sizeT - 1;
    }

    int texFmt = (texImageParam >> 26) & 0x7;

    if (texFmt == 0) {
        color = float4(oColor, 1.0);
    } else if (texFmt == 1) {
        color = a3i5Tex(polygonIndex, addrOffset, s, t, sizeS);
    } else if (texFmt == 2) {
        bool transparent0 = ((texImageParam >> 29) & 0x1) == 1;
        color = pal4Tex(polygonIndex, addrOffset, s, t, sizeS, transparent0);
    } else if (texFmt == 3) {
        bool transparent0 = ((texImageParam >> 29) & 0x1) == 1;
        color = pal16Tex(polygonIndex, addrOffset, s, t, sizeS, transparent0);
    } else if (texFmt == 4) {
        bool transparent0 = ((texImageParam >> 29) & 0x1) == 1;
        color = pal256Tex(polygonIndex, addrOffset, s, t, sizeS, transparent0);
    } else if (texFmt == 5) {
        color = float4(0.0, 1.0, 0.0, 1.0);
    } else if (texFmt == 6) {
        color = a5i3Tex(polygonIndex, addrOffset, s, t, sizeS);
    } else if (texFmt == 7) {
        color = directTex(addrOffset, s, t, sizeS);
    }
}

float4 out gl_Position : POSITION;
float3 out objPos : TEXCOORD0;
float2 out objDims : TEXCOORD1;
float2 out screenPosF : TEXCOORD2;
float2 out objAttrib0Addr : TEXCOORD3;
float2 out objAttrib2Addr : TEXCOORD4;

uniform usampler2D oamTex : TEXUNIT0;

uniform int dispCnt;

unsigned short readOam16Aligned(short addr) {
    float x = float(addr >> 2) / 255.0f;
    unsigned int value = tex2D<unsigned int>(oamTex, float2(x, 1.0));
    short shift = addr & 2;
    return (value >> (shift * 8)) & 0xFFFF;
}

static const float2 SizeLookup[12] = {
    float2(8.0, 8.0), float2(16.0, 16.0), float2(32.0, 32.0), float2(64.0, 64.0), float2(16.0, 8.0), float2(32.0, 8.0),
    float2(32.0, 16.0), float2(64.0, 32.0), float2(8.0, 16.0), float2(8.0, 32.0), float2(16.0, 32.0), float2(32.0, 64.0),
};

void main(float2 position, float oamIndex) {
    short index = short(oamIndex);

    unsigned short attrib0 = readOam16Aligned(index * 8);
    unsigned short attrib1 = readOam16Aligned(index * 8 + 2);
    unsigned short attrib2 = readOam16Aligned(index * 8 + 4);

    short oamX = attrib1 & 0x1FF;
    short oamY = attrib0 & 0xFF;

    int shape = (attrib0 >> 12) & 0xC;
    int size = (attrib1 >> 14) & 0x3;

    float2 oamDims = SizeLookup[shape | size];
    float oamWidth = oamDims.x;
    float oamHeight = oamDims.y;
    objDims = short2(oamWidth, oamHeight);

    if (oamX >= 256) {
        oamX -= 512;
    }

    if (oamY >= 192) {
        oamY -= 256;
    }

    float2 pos = position;

    bool affine = ((attrib0 >> 8) & 1) != 0;
    if (affine) {
        short affineIndex = (attrib1 >> 9) & 0x1F;
        short affineOffset = affineIndex * 0x20;
        short pa = readOam16Aligned(affineOffset + 6);
        short pb = readOam16Aligned(affineOffset + 14);
        short pc = readOam16Aligned(affineOffset + 22);
        short pd = readOam16Aligned(affineOffset + 30);
        float2x2 m = float2x2(float(pa), float(pb), float(pc), float(pd)) / 256.0;

        bool doubleAffine = ((attrib0 >> 9) & 1) != 0;
        if (doubleAffine) {
            float2 normPos = mul(m, float2(max(oamWidth * 2.0 * pos.x - 0.9, 0.0) - oamWidth, max(oamHeight * 2.0 * pos.y - 0.9, 0.0) - oamHeight));
            objPos = float3(normPos.x + oamWidth / 2.0, normPos.y + oamHeight / 2.0, oamIndex);
            oamWidth *= 2.0;
            oamHeight *= 2.0;
        } else {
            float2 normPos = mul(m, float2(max(oamWidth * pos.x - 0.5, 0.0) - oamWidth / 2.0, max(oamHeight * pos.y - 0.5, 0.0) - oamHeight / 2.0));
            objPos = float3(normPos.x + oamWidth / 2.0, normPos.y + oamHeight / 2.0, oamIndex);
        }
    } else {
        bool isVFlip = ((attrib1 >> 13) & 1) != 0;
        bool isHFlip = ((attrib1 >> 12) & 1) != 0;

        if (isVFlip) {
            pos.y = -pos.y + 1.0;
        }

        if (isHFlip) {
            pos.x = -pos.x + 1.0;
        }

        objPos = float3((oamWidth - 0.1) * pos.x, (oamHeight - 0.1) * pos.y, oamIndex);
    }

    float x = float(oamX) + oamWidth * position.x;
    float y = float(oamY) + oamHeight * position.y;

    screenPosF = float2(x / 256.0, y / 192.0);
    objAttrib0Addr = float2(oamIndex * 8.0 / 4.0 / 255.0, 1.0);
    objAttrib2Addr = float2((oamIndex * 8.0 + 4.0) / 4.0 / 255.0, 1.0);

    short priority = short((attrib2 >> 10) & 3);
    gl_Position = float4(screenPosF.x * 2.0 - 1.0, 1.0 - screenPosF.y * 2.0, float(priority) / 4.0, 1.0);
}

float2 in screenPos : TEXCOORD0;
float2 in screenPosF : TEXCOORD1;

uniform int dispCnt;

uniform WinBgUbo {
    unsigned int winHV[192 * 2];
    unsigned int winInOut[192];
} WinBgUbo : BUFFER[0];

uniform usampler2D objWinTex : TEXUNIT0;

static float4 oColor;

bool checkBounds(short x, short y, short winNum) {
    bool winEnabled = (dispCnt & (1 << (13 + winNum))) != 0;
    if (!winEnabled) {
        return false;
    }

    int hv = WinBgUbo.winHV[y * 2 + winNum];
    short h = hv & 0xFFFF;
    short v = (hv >> 16) & 0xFFFF;

    int winX1 = (h >> 8) & 0xFF;
    int winX2 = h & 0xFF;

    int winY1 = (v >> 8) & 0xFF;
    int winY2 = v & 0xFF;

    if (winX1 <= winX2) {
        if (x < winX1 || x > winX2) {
            return false;
        }
    } else {
        if (x >= winX2 && x < winX1) {
            return false;
        }
    }

    if (winY1 <= winY2) {
        if (y < winY1 || y > winY2) {
            return false;
        }
    } else {
        if (y >= winY2 && y < winY1) {
            return false;
        }
    }

    unsigned short winIn = WinBgUbo.winInOut[y] & 0xFFFF;
    unsigned short enabled = (winIn >> (winNum * 8)) & 0xFF;
    oColor = float4(float(enabled) / 255.0, 0.0, 0.0, 0.0);
    return true;
}

void main(out float4 color : COLOR) {
    short x = short(screenPos.x);
    short y = short(screenPos.y);
    unsigned short objWin = tex2D<unsigned int>(objWinTex, screenPosF);

    if (!checkBounds(x, y, 0) && !checkBounds(x, y, 1)) {
        if (((objWin >> 7) & 1) != 0) {
            objWin &= 0x7F;
            oColor = float4(float(objWin) / 255.0, 0.0, 0.0, 0.0);
        } else {
            unsigned short enabled = (WinBgUbo.winInOut[y] >> 16) & 0xFF;
            oColor = float4(float(enabled) / 255.0, 0.0, 0.0, 0.0);
        }
    }

    color = oColor;
}

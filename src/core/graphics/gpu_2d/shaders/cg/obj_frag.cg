float3 in objPos : TEXCOORD0;
float2 in objDims : TEXCOORD1;
float2 in screenPosF : TEXCOORD2;
float2 in objAttrib0Addr : TEXCOORD3;
float2 in objAttrib2Addr : TEXCOORD4;

uniform usampler2D oamTex : TEXUNIT0;
uniform usampler2D objTex : TEXUNIT1;
uniform sampler2D palTex : TEXUNIT2;
uniform sampler2D extPalTex : TEXUNIT3;
uniform usampler2D winTex : TEXUNIT4;

uniform int dispCnt;

struct ObjAttr {
    unsigned int mapWidth;
    unsigned int objBound;
};

uniform ObjUbo {
    ObjAttr attrs[128];
} ObjUbo : BUFFER[0];

unsigned short readAttrib0() {
    unsigned int value = tex2D<unsigned int>(oamTex, objAttrib0Addr);
    return value & 0xFFFF;
}

unsigned short readAttrib2() {
    unsigned int value = tex2D<unsigned int>(oamTex, objAttrib2Addr);
    return value & 0xFFFF;
}

unsigned short readObj8(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0f;
    float y = float(addrY) / (OBJ_TEX_HEIGHT - 1.0);
    unsigned int values[4];
    tex2D_gather4(objTex, float2(x, y), values);
    short shift = addr & 3;
    return (values[0] >> (shift * 8)) & 0xFF;
}

unsigned short readObj16Aligned(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0f;
    float y = float(addrY) / (OBJ_TEX_HEIGHT - 1.0);
    unsigned int values[4];
    tex2D_gather4(objTex, float2(x, y), values);
    short shift = addr & 2;
    return (values[0] >> (shift * 8)) & 0xFFFF;
}

float4 readPal(short index) {
    return tex2D(palTex, float2(float(index) / 511.0, 1.0));
}

float4 readExtPal(short index) {
    short indexX = index & 0x1FF;
    short indexY = index >> 9;
    float x = float(indexX) / 511.0;
    float y = float(indexY) / 7.0;
    return tex2D(extPalTex, float2(x, y));
}

float3 normRgb5(short color) {
    return float3(float(color & 0x1F), float((color >> 5) & 0x1F), float((color >> 10) & 0x1F)) / 31.0;
}

float4 drawSprite(short objX, short objY, unsigned short attrib0, unsigned short attrib2, ObjAttr attr) {
    int mapWidth = attr.mapWidth;
    int objBound = attr.objBound;

    int tileIndex = attrib2 & 0x3FF;
    int tileAddr = tileIndex * objBound;
    int tileAddrOffset = ((objY & 7) + (objY >> 3) * mapWidth) * 8;
    tileAddrOffset += (objX >> 3) * 64 + (objX & 7);

    bool is8bpp = ((attrib0 >> 13) & 1) != 0;
    if (!is8bpp) {
        tileAddrOffset /= 2;
    }

    int palIndex = readObj8(tileAddr + tileAddrOffset);
    short palColor;

    if (is8bpp) {
        if (palIndex == 0) {
            discard;
        }

        bool useExtPal = (dispCnt & (1 << 31)) != 0;
        if (useExtPal) {
            short palBaseAddr = ((attrib2 >> 12) & 0xF) << 9;
            short palBaseIndex = ((attrib2 >> 12) & 0xF) << 8;
            return float4(readExtPal(palBaseIndex + palIndex).rgb, 1.0);
        } else {
            return float4(readPal(0x100 + palIndex).rgb, 1.0);
        }
    } else {
        palIndex >>= 4 * (objX & 1);
        palIndex &= 0xF;
        if (palIndex == 0) {
            discard;
        }

        short palBank = (attrib2 >> 12) & 0xF;
        short palBase = 0x100 + palBank * 16;
        return float4(readPal(palBase + palIndex).rgb, 1.0);
    }
    return float4(normRgb5(palColor).rgb, 1.0);
}

float4 drawBitmap(short objX, short objY, ObjAttr attr) {
    int bitmapWidth = attr.mapWidth;
    int dataBase = attr.objBound;

    short objColor = readObj16Aligned(dataBase + (objY * bitmapWidth + objX) * 2);
    if (((objColor >> 15) & 1) == 0) {
        discard;
    }
    return float4(normRgb5(objColor), 1.0);
}

void main(out float4 color : COLOR) {
    unsigned int attrib0 = readAttrib0();
    unsigned int attrib2 = readAttrib2();
    unsigned short winEnabled = tex2D<unsigned int>(winTex, screenPosF);

    if (((winEnabled >> 4) & 1) == 0) {
        discard;
    }

    short objWidth = short(objDims.x);
    short objHeight = short(objDims.y);
    short objY = short(objPos.y);
    short objX = short(objPos.x);

    if (objX < 0 || objX >= objWidth || objY < 0 || objY >= objHeight) {
        discard;
    }

    short oamIndex = short(objPos.z);
    ObjAttr attr = ObjUbo.attrs[oamIndex];

    short gfxMode = (attrib0 >> 10) & 3;
    bool isBitmap = gfxMode == 3;
    if (isBitmap) {
        color = drawBitmap(objX, objY, attr);
    } else {
        color = drawSprite(objX, objY, attrib0, attrib2, attr);
        bool semiTransparent = gfxMode == 1;
        if (semiTransparent) {
            color.a = 0.0;
        }
    }
}

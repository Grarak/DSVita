float3 in objPos : TEXCOORD0;
float2 in objDims : TEXCOORD1;
float2 in screenPosF : TEXCOORD2;

uniform usampler2D oamTex : TEXUNIT0;
uniform usampler2D objTex : TEXUNIT1;
uniform usampler2D palTex : TEXUNIT2;
uniform usampler2D extPalTex : TEXUNIT3;
uniform usampler2D winTex : TEXUNIT4;

uniform int dispCnt;
uniform ObjUbo {
    unsigned int mapWidthsObjBounds[256];
} ObjUbo : BUFFER[0];

unsigned short readOam8(short addr) {
    float x = float(addr >> 2) / 255.0f;
    unsigned int value = tex2D<unsigned int>(oamTex, float2(x, 1.0));
    short shift = addr & 3;
    return (value >> (shift * 8)) & 0xFF;
}

unsigned short readOam16Aligned(short addr) {
    float x = float(addr >> 2) / 255.0f;
    unsigned int value = tex2D<unsigned int>(oamTex, float2(x, 0.0));
    short shift = addr & 2;
    return (value >> (shift * 8)) & 0xFFFF;
}

unsigned short readObj8(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0f;
    float y = float(addrY) / (OBJ_TEX_HEIGHT - 1.0);
    unsigned int value = tex2D<unsigned int>(objTex, float2(x, y));
    short shift = addr & 3;
    return (value >> (shift * 8)) & 0xFF;
}

unsigned short readObj16Aligned(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0f;
    float y = float(addrY) / (OBJ_TEX_HEIGHT - 1.0);
    unsigned int value = tex2D<unsigned int>(objTex, float2(x, y));
    short shift = addr & 2;
    return (value >> (shift * 8)) & 0xFFFF;
}

unsigned short readPal16Aligned(short addr) {
    float x = float(addr >> 2) / 255.0f;
    unsigned int value = tex2D<unsigned int>(palTex, float2(x, 0.0));
    short shift = addr & 2;
    return (value >> (shift * 8)) & 0xFFFF;
}

unsigned short readExtPal16Aligned(short addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0f;
    float y = float(addrY) / 3.0f;
    unsigned int value = tex2D<unsigned int>(extPalTex, float2(x, y));
    short shift = addr & 2;
    return (value >> (shift * 8)) & 0xFFFF;
}

float3 normRgb5(unsigned short color) {
    return float3(float(color & 0x1F), float((color >> 5) & 0x1F), float((color >> 10) & 0x1F)) / 31.0f;
}

float4 drawSprite(short objX, short objY, short attrib0High, short oamIndex) {
    short mapWidth = short(ObjUbo.mapWidthsObjBounds[oamIndex * 2]);
    short objBound = short(ObjUbo.mapWidthsObjBounds[oamIndex * 2 + 1]);

    short attrib2 = readOam16Aligned(oamIndex * 8 + 4);

    int tileIndex = attrib2 & 0x3FF;
    int tileAddr = tileIndex * objBound;
    int tileAddrOffset = ((objY & 7) + (objY >> 3) * mapWidth) * 8;
    tileAddrOffset += (objX >> 3) * 64 + (objX & 7);

    bool is8bpp = ((attrib0High >> 5) & 1) != 0;
    if (!is8bpp) {
        tileAddrOffset /= 2;
    }

    int palIndex = readObj8(tileAddr + tileAddrOffset);
    short palColor;

    if (is8bpp) {
        if (palIndex == 0) {
            discard;
        }

        bool useExtPal = (dispCnt & (1 << 31)) != 0;
        if (useExtPal) {
            short palBaseAddr = ((attrib2 >> 12) & 0xF) << 9;
            palColor = readExtPal16Aligned(palBaseAddr + palIndex * 2);
        } else {
            palColor = readPal16Aligned(0x200 + palIndex * 2);
        }
    } else {
        palIndex >>= 4 * (objX & 1);
        palIndex &= 0xF;
        if (palIndex == 0) {
            discard;
        }

        short palBank = (attrib2 >> 12) & 0xF;
        short palBase = 0x200 + palBank * 32;
        palColor = readPal16Aligned(palBase + palIndex * 2);
    }
    return float4(normRgb5(palColor).rgb, 1.0);
}

float4 drawBitmap(short objX, short objY, short oamIndex) {
    int bitmapWidth = ObjUbo.mapWidthsObjBounds[oamIndex * 2];
    int dataBase = ObjUbo.mapWidthsObjBounds[oamIndex * 2 + 1];

    short objColor = readObj16Aligned(dataBase + (objY * bitmapWidth + objX) * 2);
    if (((objColor >> 15) & 1) == 0) {
        discard;
    }
    return float4(normRgb5(objColor), 1.0);
}

void main(out float4 color : COLOR) {
    short objWidth = short(objDims.x);
    short objHeight = short(objDims.y);
    short objY = short(objPos.y);
    short objX = short(objPos.x);

    if (objX < 0 || objX >= objWidth || objY < 0 || objY >= objHeight) {
        discard;
    }

    unsigned short winEnabled = tex2D<unsigned int>(winTex, screenPosF);
    if (((winEnabled >> 4) & 1) == 0) {
        discard;
    }

    short oamIndex = short(objPos.z);

    short attrib0High = readOam8(oamIndex * 8 + 1);

    short gfxMode = (attrib0High >> 2) & 3;
    bool isBitmap = gfxMode == 3;
    if (isBitmap) {
        color = drawBitmap(objX, objY, oamIndex);
    } else {
        color = drawSprite(objX, objY, attrib0High, oamIndex);
        bool semiTransparent = gfxMode == 1;
        if (semiTransparent) {
            color.a = 0.0;
        }
    }
}

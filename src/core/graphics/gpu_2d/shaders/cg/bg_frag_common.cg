float3 in screenPos : TEXCOORD0;
float2 in screenPosF : TEXCOORD1;
float2 in affineDims : TEXCOORD2;

uniform int dispCnt;
uniform int bgCnt;
uniform float bgTexHeight;

uniform usampler2D bgTex : TEXUNIT0;
uniform sampler2D palTex : TEXUNIT1;
uniform sampler2D extPalTex : TEXUNIT2;
uniform usampler2D winTex : TEXUNIT3;
uniform sampler2D display3dTex : TEXUNIT4;

uniform BgUbo {
    int bgOfs[192 * 4];
    int bgX[192 * 2];
    int bgY[192 * 2];
    int bgPas[192 * 2];
    int bgPbs[192 * 2];
    int bgPcs[192 * 2];
    int bgPds[192 * 2];
} BgUbo : BUFFER[0];

unsigned short readBg8(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0;
    float y = float(addrY) / bgTexHeight;
    unsigned int values[4];
    tex2D_gather4(bgTex, float2(x, y), values);
    short shift = addr & 3;
    return (values[0] >> (shift * 8)) & 0xFF;
}

unsigned short readBg16Aligned(int addr) {
    short addrX = (addr >> 2) & 0x1FF;
    short addrY = addr >> 11;
    float x = float(addrX) / 511.0;
    float y = float(addrY) / bgTexHeight;
    unsigned int values[4];
    tex2D_gather4(bgTex, float2(x, y), values);
    short shift = addr & 2;
    return (values[0] >> (shift * 8)) & 0xFFFF;
}

float4 readPal(short index) {
    return tex2D(palTex, float2(float(index) / 511.0, 1.0));
}

float4 readExtPal(short index) {
    short indexX = index & 0x1FF;
    short indexY = index >> 9;
    float x = float(indexX) / 511.0;
    float y = float(indexY) / 31.0;
    return tex2D(extPalTex, float2(x, y));
}

float3 normRgb5(unsigned short color) {
    return float3(float(color & 0x1F), float((color >> 5) & 0x1F), float((color >> 10) & 0x1F)) / 31.0;
}

short2 calculateAffineCoords(short x, short y, short bgNum) {
    short index = (bgNum - 2) * 192 + y;
    float bgX = float(BgUbo.bgX[index]) / 256.0;
    float bgY = float(BgUbo.bgY[index]) / 256.0;
    float bgPa = float(BgUbo.bgPas[index]) / 256.0;
    float bgPb = float(BgUbo.bgPbs[index]) / 256.0;
    float bgPc = float(BgUbo.bgPcs[index]) / 256.0;
    float bgPd = float(BgUbo.bgPds[index]) / 256.0;
    return short2(short(bgX + bgPb + float(x) * bgPa), short(bgY + bgPd + float(x) * bgPc));
}

bool can_display(unsigned short bgNum) {
    unsigned short winEnabled = tex2D<unsigned int>(winTex, screenPosF);
    return ((winEnabled >> bgNum) & 1) != 0;
}

float4 setPrio(float4 color) {
    short priority = short(bgCnt & 3);
    color.a = float(priority) / 255.0;
    return color;
}

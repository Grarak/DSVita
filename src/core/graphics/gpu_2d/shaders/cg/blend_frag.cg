float2 in screenPos : TEXCOORD0;

uniform sampler2D bg0Tex : TEXUNIT0;
uniform sampler2D bg1Tex : TEXUNIT1;
uniform sampler2D bg2Tex : TEXUNIT2;
uniform sampler2D bg3Tex : TEXUNIT3;
uniform sampler2D objTex : TEXUNIT4;
uniform sampler2D objDepthTex : TEXUNIT5;
uniform usampler2D winTex : TEXUNIT6;

uniform BlendUbo {
    int bldCntsAlphasYs[192];
} BlendUbo : BUFFER[0];

static char topNum = 5;
static char bottomNum = 5;
static bool top3D = false;
static char topPrio = 4;
static char bottomPrio = 4;
static float4 topColor = float4(0.0, 0.0, 0.0, 1.0);
static float4 bottomColor = float4(0.0, 0.0, 0.0, 1.0);

void sortObjPrio() {
    char prio = char((tex2D<float>(objDepthTex, screenPos) * 2.0 - 1.0) * 4.0);
    if (prio < topPrio) {
        topNum = 4;
        topPrio = prio;
        topColor = tex2D(objTex, screenPos);
    }
}

void sortBgPrio(short num, sampler2D bgTex) {
    float4 texColor = tex2D(bgTex, screenPos);
    unsigned char data = unsigned char(texColor.a * 255.0);
    if (data == 255) { // frag was discarded
        return;
    }

    unsigned char prio = data & 0x3;
    if (prio < topPrio) {
        bottomNum = topNum;
        bottomPrio = topPrio;
        bottomColor = topColor;

        topNum = num;
        topPrio = prio;
        topColor = texColor;
        unsigned char alpha3D = (data >> 2) & 0x3F;
        top3D = num == 0 && alpha3D != 0;
        if (top3D) {
            topColor.a = float(alpha3D) / 31.0;
        }
    } else if (prio < bottomPrio) {
        bottomNum = num;
        bottomPrio = prio;
        bottomColor = texColor;
    }
}

float4 alphaBlend(short eva, short evb) {
    float evaF = float(eva) / 16.0;
    float evbF = float(evb) / 16.0;
    float3 blendedColor = topColor.rgb * evaF + bottomColor.rgb * evbF;
    return float4(blendedColor.rgb, 1.0);
}

void main(out float4 color : COLOR) {
    unsigned short winEnabled = tex2D<unsigned int>(winTex, screenPos);

    sortObjPrio();
    sortBgPrio(0, bg0Tex);
    sortBgPrio(1, bg1Tex);
    sortBgPrio(2, bg2Tex);
    sortBgPrio(3, bg3Tex);

    if (topNum == 5) {
        discard;
    }

    short y = short(screenPos.y * 191.0);
    int bldCntAlphaY = BlendUbo.bldCntsAlphasYs[y];
    short bldCnt = bldCntAlphaY & 0xFFFF;
    short bldEva = (bldCntAlphaY >> 16) & 0x1F;
    short bldEvb = (bldCntAlphaY >> 21) & 0x1F;
    short bldY = (bldCntAlphaY >> 26) & 0x1F;
    bool blendTop = ((bldCnt >> topNum) & 1) != 0;
    bool blendBottom = ((bldCnt >> (8 + bottomNum)) & 1) != 0;
    unsigned char bldMode = (bldCnt >> 6) & 3;

    if (top3D) {
        if (blendBottom) {
            float eva = topColor.a;
            float evb = 1.0 - eva;
            color = float4(topColor.rgb * eva + bottomColor.rgb * evb, 1.0);
            return;
        }

        if (bldMode < 2) {
            bldMode = 0;
        }
    } else if (topNum == 4 && topColor.a == 0.0) {
        // Semi transparent object
        if (blendBottom) {
            color = alphaBlend(bldEva, bldEvb);
            return;
        }

        if (bldMode < 2) {
            bldMode = 0;
        }
    }

    if (bldMode == 0 || !blendTop || ((winEnabled >> 5) & 1) == 0) {
        color = float4(topColor.rgb, 1.0);
        return;
    }

    if (bldMode == 1) {
        if (blendBottom) {
            color = alphaBlend(bldEva, bldEvb);
        } else {
            color = float4(topColor.rgb, 1.0);
        }
    } else if (bldMode == 2) {
        float bldYF = float(bldY) / 16.0;
        float3 increaseColor = (1.0 - topColor.rgb) * bldYF;
        color = float4((topColor.rgb + increaseColor), 1.0);
    } else if (bldMode == 3) {
        float bldYF = float(bldY) / 16.0;
        float3 decreaseColor = topColor.rgb * bldYF;
        color = float4((topColor.rgb - decreaseColor), 1.0);
    }
}
